PREFIX : @schemaUri
PREFIX id: @instanceUri


CONSTRUCT {
  ?statutoryInstrumentPaper 
    	a :StatutoryInstrumentPaper ;
    	:statutoryInstrumentPaperName ?statutoryInstrumentPaperName ;
    	:laidThingHasLaying ?laying . 	
  ?workPackage
    	a :WorkPackage ;
  		:workPackageHasWorkPackagedThing ?statutoryInstrumentPaper .
  ?laying
    	a :Laying ;
  		:layingDate ?layingDate .
}
WHERE {
  {
    SELECT ?workPackage ?statutoryInstrumentPaper WHERE {
      BIND(id:u5AUJb2q AS ?procedureStep1)
      BIND(id:hN1EDPLv AS ?procedureStep2)
      
      ?statutoryInstrumentPaper a :StatutoryInstrumentPaper ;
  							:workPackagedThingHasWorkPackage ?workPackage .
  	
      FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?procedureStep1 . }
      FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?procedureStep2 . }
    }
  }	
  
  OPTIONAL { ?statutoryInstrumentPaper :statutoryInstrumentPaperName ?statutoryInstrumentPaperName . }
  OPTIONAL { 
    ?statutoryInstrumentPaper :laidThingHasLaying ?laying . 
  	?laying :layingDate ?layingDate . 
  }
}