PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : @schemaUri

CONSTRUCT {
	?proposedNegativeStatutoryInstrumentPaper
		a :ProposedNegativeStatutoryInstrumentPaper ;
		:proposedNegativeStatutoryInstrumentPaperName ?proposedNegativeStatutoryInstrumentPaperName ;
		:workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink ;
		:proposedNegativeStatutoryInstrumentPaperPrecedesStatutoryInstrumentPaper ?statutoryInstrumentPaper ;
		:workPackagedThingHasWorkPackage ?workPackage .

	?statutoryInstrumentPaper
		a :StatutoryInstrumentPaper ;
		:statutoryInstrumentPaperName ?statutoryInstrumentPaperName .

	?workPackage
		a :WorkPackage ;
		:workPackageHasBusinessItem ?businessItem ;
		:workPackageHasProcedure ?procedure .

	?businessItem
		a :BusinessItem, ?laying ;
		:businessItemDate ?businessItemDate ;
		:layingDate ?layingDate ;
		:layingHasLayingBody ?layingBody ;
		:layingHasLayingPerson ?layingPerson ;
		:businessItemHasBusinessItemWebLink ?businessItemWebLink .

	?procedure
		a :Procedure ;
		:procedureName ?procedureName .

	?layingBody
		a :LayingBody, :Group ;
		:groupName ?groupName .

	?layingPerson
    	a :Person ;
    	:personGivenName ?givenName ;
        :personFamilyName ?familyName ;
        <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs ;
        :memberHasMemberImage ?personImage .

    ?personImage
        a :MemberImage .

}
WHERE {
	BIND (@proposed_negative_statutory_instrument_paper_id as ?proposedNegativeStatutoryInstrumentPaper)

	?proposedNegativeStatutoryInstrumentPaper a :ProposedNegativeStatutoryInstrumentPaper .

	OPTIONAL { ?proposedNegativeStatutoryInstrumentPaper :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink . }
	OPTIONAL { ?proposedNegativeStatutoryInstrumentPaper :proposedNegativeStatutoryInstrumentPaperName ?proposedNegativeStatutoryInstrumentPaperName . }

	OPTIONAL {
		?proposedNegativeStatutoryInstrumentPaper :proposedNegativeStatutoryInstrumentPaperPrecedesStatutoryInstrumentPaper ?statutoryInstrumentPaper .
		OPTIONAL { ?statutoryInstrumentPaper :statutoryInstrumentPaperName ?statutoryInstrumentPaperName . }
	}
	OPTIONAL {
		?proposedNegativeStatutoryInstrumentPaper :workPackagedThingHasWorkPackage ?workPackage .

		OPTIONAL{
			?workPackage :workPackageHasProcedure ?procedure .
			?procedure :procedureName ?procedureName .
		}

		OPTIONAL {
			?workPackage :workPackageHasBusinessItem ?businessItem .
			OPTIONAL { ?businessItem :businessItemDate ?businessItemDate . }
			OPTIONAL { ?businessItem :businessItemHasBusinessItemWebLink ?businessItemWebLink . }
			OPTIONAL {
				BIND (:Laying as ?laying)
				?businessItem a ?laying .

				?businessItem :layingHasLayingBody ?layingBody .
				OPTIONAL { ?layingBody :groupName ?groupName . }
				OPTIONAL { ?businessItem :layingDate ?layingDate . }
				OPTIONAL {
                    ?businessItem :layingHasLayingPerson ?layingPerson ;

                    OPTIONAL { ?layingPerson :personGivenName ?givenName . }
                    OPTIONAL { ?layingPerson :personFamilyName ?familyName . }
                    OPTIONAL { ?layingPerson <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs . }
                    OPTIONAL { ?layingPerson :memberHasMemberImage ?personImage . }
                }
			}
		}
	}
}