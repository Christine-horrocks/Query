PREFIX : @schemaUri

CONSTRUCT {
  ?procedure
      a :Procedure ;
      :procedureName ?procedureName ;
      :procedureHasWorkPackage ?workPackage ;
  .
  ?workPackage
      a :WorkPackage ;
      :workPackageHasWorkPackageableThing ?workPackageableThing ;
      :workPackageHasBusinessItem ?businessItem ;
  .
  ?workPackageableThing
      a :WorkPackageableThing ;
      :workPackageableThingName ?workPackageableThingName ;
      :workPackageableThingHasWorkPackage ?workPackage ;
  .
  ?businessItem
      a :BusinessItem ;
      :businessItemDate ?businessItemDate ;
  .
}
WHERE {
  {
      SELECT * WHERE {
        BIND (@procedure_id AS ?procedure)
        ?procedure
            a :Procedure ;
            :procedureName ?procedureName ;
        .
        OPTIONAL {
            ?procedure :procedureHasWorkPackage ?workPackage .
            OPTIONAL {
                ?workPackage :workPackageHasWorkPackageableThing ?workPackageableThing .
                OPTIONAL { ?workPackageableThing :workPackageableThingName ?workPackageableThingName . }
                OPTIONAL { ?workPackageableThing :workPackageableThingHasWorkPackage ?workPackage . }
            }
            OPTIONAL {
                ?workPackage :workPackageHasBusinessItem ?businessItem .
                ?businessItem :businessItemDate ?businessItemDate .
            }
        }
      }
  }
}
