PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
    ?procedure
        a :Procedure ;
        :procedureName ?procedureName ;
        :procedureHasWorkPackage ?package ;
    .

    ?package
        a :WorkPackage ;
        :workPackageHasWorkPackageableThing ?packageable ;
        ex:oldestBusinessItemDate ?date ;
    .

    ?packageable
        a :WorkPackageableThing ;
        :workPackageableThingName ?packageableName ;
    .
}
WHERE {
    BIND (@procedure_id AS ?procedure)

    ?procedure :procedureName ?procedureName .

    OPTIONAL {
        ?package :workPackageHasProcedure ?procedure .

        # Date (if any) of oldest business item in package
        {
            SELECT ?package (MIN(?itemDate) AS ?date)
            WHERE {
                ?item :businessItemHasWorkPackage ?package .

                OPTIONAL { ?item :businessItemDate ?itemDate . }
            }
            GROUP BY ?package
        }

        ?packageable
            :workPackageableThingHasWorkPackage ?package ;
            :workPackageableThingName ?packageableName ;
        .
    }
}
