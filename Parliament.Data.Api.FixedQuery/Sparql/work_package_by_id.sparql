PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
	?workPackageableThing
		a :WorkPackageableThing ;
		:workPackageableThingName ?workPackageableThingName ;
		:workPackageableThingHasWorkPackageableThingWebLink ?workPackageableThingWebLink ;
		:workPackageableThingHasWorkPackage ?workPackage ;
	.

	?workPackage
		a :WorkPackage ;
		:workPackageHasBusinessItem ?actualizedItem ;
	.

	?procedure
		a :Procedure ;
		:procedureHasWorkPackage ?workPackage ;
		:procedureName ?procedureName ;
	.

	?possibleStep a :ProcedureStep, ex:POSSIBLE .
	?actualizedStep a :ProcedureStep, ex:ACTUALIZED .
	?rootStep a ex:ROOT .

	?actualizedStep ex:LEDTO ?otherActualizedStep .
	?actualizedStep ex:CANLEADTO ?otherActualizedStep .
	?enablingStep ex:ENABLES ?possibleStep .
	?enablingStep ex:CANLEADTO ?possibleStep .

	?actualizedStep :procedureStepName ?actualizedStepName .
	?actualizedStep :procedureStepHasHouse ?actualizedStepHouse .

	?actualizedStepHouse
		a :House ;
		:houseName ?houseName ;
	.

	?possibleStep :procedureStepName ?possibleName .

	?actualizedItem a :BusinessItem .
	?actualizedItem ex:ACTUALIZED ?actualizedStep .
  ?actualizedItem :businessItemDate ?actualizedItemDate .
  ?actualizedItem :businessItemHasBusinessItemWebLink ?actualizedItemWebLink .
  ?actualizedItem :businessItemHasWorkPackage ?workPackage .
  ?actualizedItem :businessItemHasProcedureStep ?actualizedStep .

}
WHERE {
	BIND(@work_package_id AS ?workPackageableThing)
	?workPackageableThing
		:workPackageableThingName ?workPackageableThingName ;
		:workPackageableThingHasWorkPackage ?workPackage ;
	.

	OPTIONAL { ?workPackageableThing :workPackageableThingHasWorkPackageableThingWebLink ?workPackageableThingWebLink . }

	?procedure
		:procedureHasWorkPackage ?workPackage ;
		:procedureName ?procedureName ;
	.

  {
    ?enablingStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .

    ?possibleRoute :procedureRouteHasProcedure ?procedure .
    ?possibleRoute :procedureRouteIsFromProcedureStep ?enablingStep .
    ?possibleRoute :procedureRouteIsToProcedureStep ?possibleStep .

    ?possibleStep :procedureStepName ?possibleName .

    FILTER NOT EXISTS {
      ?possibleStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .
    }

    FILTER NOT EXISTS {
      ?precludingRoute :procedureRouteIsToProcedureStep ?possibleStep .
      ?precludingRoute :procedureRouteHasProcedure ?procedure .
      ?precludingRoute :precludedProcedureRouteIsPrecludedByProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .
    }

    FILTER NOT EXISTS {
      ?reqRoute :requiredProcedureRouteIsRequiredByProcedureStep ?possibleStep .
      ?reqRoute :procedureRouteHasProcedure ?procedure .

      FILTER NOT EXISTS {
        ?reqRoute :procedureRouteIsToProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .
      }
    }
  }
  UNION
  {
    ?workPackage :workPackageHasBusinessItem ?actualizedItem .
    ?actualizedItem :businessItemHasWorkPackage ?workPackage .
    ?actualizedItem :businessItemHasProcedureStep ?actualizedStep .

    OPTIONAL { ?actualizedItem :businessItemDate ?actualizedItemDate . }
    OPTIONAL { ?actualizedItem :businessItemHasBusinessItemWebLink ?actualizedItemWebLink . }

    ?actualizedStep :procedureStepName ?actualizedStepName .
    ?actualizedStep :procedureStepHasHouse ?actualizedStepHouse .

    ?actualizedStepHouse :houseName ?houseName .

    OPTIONAL {
      ?actualizedRoute :procedureRouteIsFromProcedureStep ?actualizedStep .
      ?actualizedRoute :procedureRouteIsToProcedureStep ?otherActualizedStep .
      ?actualizedRoute :procedureRouteHasProcedure ?procedure .

      ?otherActualizedStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .

      FILTER (?otherActualizedStep != ?actualizedStep)
    }
  }
  UNION
  {
    ?rootItem :businessItemHasWorkPackage ?workPackage .
    ?rootItem :businessItemHasProcedureStep ?rootStep .

    FILTER NOT EXISTS {
      ?higherRoute :procedureRouteIsToProcedureStep ?rootStep .
      ?higherRoute :procedureRouteHasProcedure ?procedure .
      ?higherRoute (:causedProcedureRouteIsCausedByProcedureStep|:allowedProcedureRouteIsAllowedByProcedureStep)/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?workPackage .
    }
  }
}
