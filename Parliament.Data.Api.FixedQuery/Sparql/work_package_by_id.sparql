PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
	?actualizedStep
		a :ProcedureStep, ex:Actualized ;
		:procedureStepName ?actualizedStepName ;
		ex:ledTo ?otherActualizedStep ;
		ex:canLeadTo ?otherActualizedStep ;
	.

	?possibleStep
		a :ProcedureStep, ex:Possible ;
		:procedureStepName ?possibleName ;
	.

	?enablingStep
		ex:enables ?possibleStep ;
		ex:canLeadTo ?possibleStep ;
	.

	?rootStep a ex:Root .

	?actualizedItem :businessItemHasProcedureStep ?actualizedStep .
}
WHERE {
  BIND(@work_package_id AS ?package)

  ?procedure :procedureHasWorkPackage ?package .

  {
    ?enablingStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .

    ?possibleRoute :procedureRouteHasProcedure ?procedure .
    ?possibleRoute :procedureRouteIsFromProcedureStep ?enablingStep .
    ?possibleRoute :procedureRouteIsToProcedureStep ?possibleStep .

    ?possibleStep :procedureStepName ?possibleName .

    FILTER NOT EXISTS {
      ?possibleStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    }

    FILTER NOT EXISTS {
      ?precludingRoute :procedureRouteIsToProcedureStep ?possibleStep .
      ?precludingRoute :procedureRouteHasProcedure ?procedure .
      ?precludingRoute :precludedProcedureRouteIsPrecludedByProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    }

    FILTER NOT EXISTS {
      ?reqRoute :requiredProcedureRouteIsRequiredByProcedureStep ?possibleStep .
      ?reqRoute :procedureRouteHasProcedure ?procedure .

      FILTER NOT EXISTS {
        ?reqRoute :procedureRouteIsToProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
      }
    }
  }
  UNION 
  {
    ?actualizedItem :businessItemHasWorkPackage ?package .
    ?actualizedItem :businessItemHasProcedureStep ?actualizedStep .

    ?actualizedStep :procedureStepName ?actualizedStepName .

    OPTIONAL {
      ?actualizedRoute :procedureRouteIsFromProcedureStep ?actualizedStep .
      ?actualizedRoute :procedureRouteIsToProcedureStep ?otherActualizedStep .
      ?actualizedRoute :procedureRouteHasProcedure ?procedure .

      ?otherActualizedStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .

      FILTER (?otherActualizedStep != ?actualizedStep)
    }
  }
  UNION 
  {
    ?rootItem :businessItemHasWorkPackage ?package .
    ?rootItem :businessItemHasProcedureStep ?rootStep .

    FILTER NOT EXISTS {
      ?higherRoute :procedureRouteIsToProcedureStep ?rootStep .
      ?higherRoute :procedureRouteHasProcedure ?procedure .
      ?higherRoute (:causedProcedureRouteIsCausedByProcedureStep|:allowedProcedureRouteIsAllowedByProcedureStep)/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    }
  }
}
