PREFIX : @schemaUri

CONSTRUCT {
	?workPackageableThing 
		a :WorkPackageableThing, ?statutoryInstrument, ?proposedStatutoryInstrument ;
		:workPackageableThingHasWorkPackage ?workPackage ;
		:workPackageableThingName ?workPackageableThingName ;
	.
	?workPackage
		a :WorkPackage ;
		:workPackageHasProcedure ?procedure ;
		:workPackageHasBusinessItem ?businessItem ;
	.
	?procedure
		a :Procedure ;
		:procedureName ?procedureName ;
	.
	?businessItem
		a :BusinessItem ;
		:businessItemDate ?layingBusinessItemDate ;
	.
}
WHERE {
	?workPackageableThing :workPackageableThingName ?workPackageableThingName .
	OPTIONAL {
		?workPackageableThing :workPackageableThingHasWorkPackage ?workPackage .
		OPTIONAL {
			?workPackage :workPackageHasProcedure ?procedure .
			?procedure :procedureName ?procedureName .
		}
		OPTIONAL {
			?workPackage :workPackageHasBusinessItem ?businessItem .
			?businessItem :businessItemDate ?layingBusinessItemDate .
		}
	}
}
