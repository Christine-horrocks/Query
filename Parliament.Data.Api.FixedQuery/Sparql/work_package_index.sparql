PREFIX : @schemaUri

CONSTRUCT {
	?workPackage
		a :WorkPackage ;
		:workPackageHasProcedure ?procedure ;
		:workPackageHasBusinessItem ?businessItem ;
		:workPackageHasWorkPackageableThing ?workPackageableThing ;
	.
	?workPackageableThing 
		a :WorkPackageableThing ;
		:workPackageableThingName ?workPackageableThingName ;
		:workPackageableThingHasWorkPackage ?workPackage ;
	.
	?procedure
		a :Procedure ;
		:procedureName ?procedureName ;
	.
	?businessItem
		a :BusinessItem ;
		:businessItemDate ?businessItemDate ;
	.
}
WHERE {
	?workPackage a :WorkPackage
	
	OPTIONAL { 
		?workPackage :workPackageHasWorkPackageableThing ?workPackageableThing .
		?workPackageableThing :workPackageableThingName ?workPackageableThingName .
		?workPackageableThing :workPackageableThingHasWorkPackage ?workPackage .
	}

	OPTIONAL {
		?workPackage :workPackageHasProcedure ?procedure .
		?procedure :procedureName ?procedureName .
	}
	OPTIONAL {
		?workPackage :workPackageHasBusinessItem ?businessItem .
		?businessItem :businessItemDate ?businessItemDate .
	}
}
