PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
    ?package
        a :WorkPackage ;
        :workPackageHasProcedure ?procedure ;
        :workPackageHasWorkPackageableThing ?packageable ;
        ex:oldestBusinessItemDate ?date ;
    .

    ?packageable 
        a :WorkPackageableThing ;
        :workPackageableThingName ?packageableName ;
        :workPackageableThingHasWorkPackage ?package ;
    .

    ?procedure
        a :Procedure ;
        :procedureName ?procedureName ;
    .
}
WHERE {
    ?package :workPackageHasProcedure ?procedure .
    ?procedure :procedureName ?procedureName .

    # Date (if any) of oldest business item in package
    {
        SELECT ?package (MIN(?itemDate) AS ?date)
        WHERE {
            ?item :businessItemHasWorkPackage ?package .

            OPTIONAL { ?item :businessItemDate ?itemDate . }
        }
        GROUP BY ?package
    }

    OPTIONAL { 
        ?packageable
            :workPackageableThingHasWorkPackage ?package ;
            :workPackageableThingName ?packageableName ;
        .
    }
}